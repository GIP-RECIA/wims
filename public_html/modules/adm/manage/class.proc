
!bound job2 within first,erase,erase2,modify default first

!if $jobclass=$empty
  job2=first
!endif

classcnt=!recordcnt wimshome/log/classes/.index

!if $job2=first
  !readproc class/search.proc
  !if $overflow!=yes
    !readproc scripts/ccaccount.proc
    test=!record 0 of wimshome/s2/$wims_session/$bst_since
    n=!linecnt $test
    !if $n>0
      actives=yes
      !for i=1 to $n
        l=!line $i of $test
        !distribute item $l into c,t
        !if $c isitemof $list_cl_id
          t=$[floor(($t+30)/60)]
          activ_$c=$t
        !endif
      !next i
    !endif
  !endif
!endif

!if $job2!=first
  !defread wimshome/log/classes/$jobclass/.def
!endif

!if $job2=erase2
  year=!char 1 to 4 of $wims_now
  !sh cd $wims_home/log/classes\
    if [ -d $wims_home/log/ccaccount/$jobclass ]; then\
     tar -cf $jobclass/account.tar -C $wims_home/log ccaccount/$jobclass 2>/dev/null\
    fi\
    mkdir -p $wims_home/backup/oldclasses/$year\
    tar -czf $wims_home/backup/oldclasses/$year/$jobclass.tgz $jobclass\
    rm -Rf $jobclass $wims_home/log/ccaccount/$jobclass\
    ./.build-index $jobclass
    classcnt=!recordcnt wimshome/log/classes/.index
  job2=first
!endif

!if $job2=modify
  !if $abandon!=$empty
    job2=first
    !exit
  !endif
  !if $save!=$empty
    !bound clexp between integer 19000101 and 24000101
    clpass2=!word 1 of $clpass2
    clpass2crypt=!passcrypt $clpass2
    !setdef !set class_password=$clpass2crypt\
!set class_expiration=$clexp\
!set class_secure=$clsecure in wimshome/log/classes/$jobclass/.def
    clpass=!trim $clpass
    !if $clpass!=$empty
      clpasscrypt=!passcrypt $clpass
      !setdef !set user_password=$clpasscrypt in wimshome/log/classes/$jobclass/supervisor
    !endif
    class_password=$clpass2
    class_expiration=$clexp
    class_secure=$clsecure
    !reset clexp clsecure clpass clpasscrypt clpass2 clpass2crypt
  !endif
!endif

