
pass=!mexec scripts/getpass.sh log/.wimspass
pass=!word 1 of $pass
!if * notin $pass
  passone=!passcrypt $pass
  !mexec scripts/setpass.sh log/.wimspass
  pass=$passone
  !reset passone
!endif
oncename=tmp/log/.wimspassone

auth=!word 1 of $auth
!if $auth=$empty
  !if $pass=$empty
    chars=abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789
    n=!charcnt $chars
    len=!randint 8,12
    passone=
    !for i=1 to $len
      t=!randint 1,$n
      c=!char $t of $chars
      passone=$passone$c
    !next i
    !mexec scripts/setpass.sh $oncename
  !endif
  !exit
!endif

!if $pass=$empty
  pass=!mexec scripts/getpass.sh $oncename
  pass=!passcrypt $pass
!endif
pass_check=!passcheck $auth and $pass
!if $pass_check=yes and $pass!=$empty
  !writefile wimshome/$wims_sesdir/.manager yes
  wims_ismanager=2
  job=list
  !readproc geolocoption.proc
!else
  job=wrongpass
!endif
!reset passone
