!readproc adm/class/initclass $wims_class
!if ($wims_supertype=2 and $type_class=2) or ($wims_supertype=4 and $type_class=4) or $wims_supertype=0
  reclist=exo,sheet,exam,doc,forum,user,score,cdt,livret,userphoto
!else
  reclist=exo,sheet,exam,doc,forum,user,score,cdt,livret
!endif
dirlist=src def score noscore exams sheets .users cdt livret photoboard
cls=log/classes/$wims_class
!readdef wimshome/log/classes/$wims_class/neighbors
sharing=
!for r in $sharelist
  !if $(sharing_$r)!=$empty
    sharing=!append item $r to $sharing
  !endif
!next r

!read scripts/reccnt.proc cl $cls

!if $clean!=$empty and $cl_testcnt<1
  sel=!replace item all by $reclist in $clean
  sel=!replace item sheet by sheet,exam in $sel
  sel=!replace item user by user,score,userphoto in $sel
  sel=!listintersect $sel and $reclist

  sel=!listcomplement $sharing in $sel
  sel=!listuniq $sel
  selw=!items2words $sel
  dictionary=$module_dir/arch/recdir
  dirs=!exec translator $selw
  dirs=!words2items $dirs
  dirs=!listuniq $dirs
  dirs=!items2words $dirs
  !distribute words y y y y y y y y y y y y y y y y y y y y y y y into $sel
  !if / isin $wims_class and $user=y and $class_type=1
    !! cleaning user access in case of portal or  groupement
    nb=!recordcnt wimshome/log/classes/$wims_class/.userlist
    !for k=1 to $nb
      dt=!record $k of wimshome/log/classes/$wims_class/.userlist
      u=!defof user_participate in wimshome/log/classes/$wims_superclass/.users/$(dt[3])
      u=!listcomplement $wims_class in $u
      !setdef !set user_participate=$u in wimshome/log/classes/$wims_superclass/.users/$(dt[3])
    !next k
  !endif
  !if $dirs!=$empty
    !mexec scripts/clean.sh
    wims_class_log=Clean $sel by $wims_realuser
  !endif
  !read scripts/reccnt.proc cl $cls
!endif
