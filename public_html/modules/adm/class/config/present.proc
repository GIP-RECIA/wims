
tmp_themelist=!sh find themes -name visitor.phtml | sed 's!^themes/!!;s!/visitor.phtml$!!'
tmp_themelist=!words2items $tmp_themelist
themelist=$empty
!for i in $tmp_themelist
  test=!defof theme_hide in wimshome/public_html/themes/$i/.def
  !if yes notin $test
    themelist=!append item $i to $themelist
  !endif
!next i
iconlist=!sh cd gifs/themes; ls -d */ | sort -f
iconlist=!replace internal / by in $iconlist
iconlist=!words2items $iconlist
!for icon_theme in $iconlist
  !set icon_title=!defof title in wimshome/public_html/gifs/themes/$icon_theme/INDEX
  !set iconpromptlist=!append item $icon_title to $iconpromptlist
!next
csslist=!words2items -theme- class ----
cssfile=!record 0 of wimshome/log/classes/$wims_class/css
bgimgs=!filelist gifs/bg

!if $save!=$empty
  !read scripts/setdefr.proc
  !defread wimshome/log/classes/$wims_class/.def
  cssfile=$pr_cssfile
  wims_class_log=Modify class presentation by $wims_realuser
!endif

!!read adm/du $wims_home/log/classes/$wims_class

!for cl in bgcolor,ref_menucolor,ref_button_bgcolor,ref_menucolor,\
  ref_button_bgcolor,ref_button_color,ref_button_help_color,\
  ref_button_help_bgcolor,pr_bgcolor

  !default class_$cl=$(wims_$cl)
  !default class_$cl=!defof DF_$cl in wimshome/public_html/bases/sys/defaults.conf

  !!for compatibility
  !set class_$cl=!replace internal white by #FFFFFF in $(class_$cl)
  !set class_$cl=!replace internal red by #FF0000 in $(class_$cl)
  !set class_$cl=!replace internal orange by #FFA500 in $(class_$cl)
  !set class_$cl=!replace internal yellow by #FFFF00 in $(class_$cl)
  !set class_$cl=!replace internal green by #00FF00 in $(class_$cl)
!next

!! Sadly, wims_refcolor does not exist : it is wims_ref_bgcolor
!default class_refcolor=$wims_ref_bgcolor
