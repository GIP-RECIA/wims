!if $cdt_supervise=no
  <div class="wims_msg info">
    !if $wims_user=supervisor
      $name_nosupervise
    !else
      $name_nosuperviseforuser
    !endif
  </div>
!endif
!if $cdt_description!=$empty
  <br>$cdt_description
!endif
!read proc/searchform.phtml

<div class="wims_content">
<div class="wimscenter bold">
 $name_workdone
!if $jour_begin!=$jour_end
  $name_between <span class="wims_emph">
  !read date.phtml $jour_begin,notime
  </span>
  $name_and <span class="wims_emph">
  !read date.phtml $jour_end,notime
  </span>
!else
  $name_the <span class="wims_emph">
  !read date.phtml $jour_begin,notime
  </span>
!endif
</div>


<br class="spacer">

!if $cdt_nbdate=0
  $name_noinfo1.
  !goto end
!else
  !if $nbligne=0
    !if $jour_begin=$jour_end
      $name_noinfo2.
    !else
      $name_noinfo3.
    !endif
    !goto end
  !endif
!endif

!set table_class=sortable
!set table_id=TABLE_cdt_work
$table_header
<thead>
  $table_hdtr
    <th>$name_Day</th>
    !if no isin $cdt_supervise
      <th>$name_discipline</th>
    !endif
    <th>$name_inclass</th><th>$name_worktodo</th><th style="width:30px">$name_for</th>
    !if $wims_user=supervisor and yes isin $cdt_supervise
      <th>$wims_name_action</th>
    !endif
  </tr>
</thead>
<tbody>
!for i=1 to $nbligne
  !!  !let mem_tr=$table_tr
  !if $cdt_reversedate=1
    !let don=!line $nbligne+1-$i of $list
  !else
    !let don=!line $i of $list
  !endif
  !distribute item $don into jour,file,matiere
  !let m_filedir=$wims_ref_name?session=$wims_session&cmd=getfile&special_parm=cdt/$file
  !let file=wimshome/log/classes/$file/cdt/def/$jour
  !read $file
  !let nbtodo=!itemcnt $date_todo
  !let snbtodo=$[max(1,$nbtodo)]
  <tr class="$(table_colors[$i%2+1])">
   <td rowspan="$snbtodo">
    !read date.phtml $jour,notime $dateoption
   </td>
  !if no isin $cdt_supervise
    <td rowspan="$snbtodo">$matiere</td>
  !endif
  <td rowspan="$snbtodo" valign="top">
    !read $file content
  </td>
  !if $nbtodo>0
    <td valign="top">
      !read $file todo1
    </td>
    <td>
      !read date.phtml $date_todo,notime $dateoption
    </td>
  !else
    <td>&nbsp;</td><td>&nbsp;</td>
  !endif
  !if $wims_user=supervisor and yes isin $cdt_supervise
    <td rowspan="$snbtodo">
    !set wims_ref_class=wims_button
    !href cmd=new&job=adddate&filename=$jour $wims_name_change
    $
    !set wims_ref_class=wims_button wims_warning
    !href cmd=new&job=del&filename=$jour $wims_name_erase
    </td>
  !endif
  </tr>
  !if $nbtodo>1
    !let bgcolor=!positionof char # in $mem_tr
    !let bgcolor=!char $bgcolor to $[$bgcolor+6] of $mem_tr
    !for j=2 to $nbtodo
      <tr class="$(table_colors[$i%2+1])">
      <td valign="top">
      !read $file todo$j
      </td>
      <td>
      !let tmp=!item $j of $date_todo
      !read date.phtml $tmp,notime $dateoption
      </td>
      </tr>
    !next j
  !endif
!next i
</tbody>
$table_end
!read tablesort.phtml

!if $nbligne>0
  <p class="wimscenter">
  !set wims_ref_class=wims_button
  !href target=wims_new module=$module&cmd=new&job=work&job2=print&jdate=$jdate&jdate2=$jdate2 $wims_name_print
  </p>
  !set wims_menu_items=!append line printable,1,module=$module&cmd=new&job=work&job2=print&jdate=$jdate&jdate2=$jdate2\
  itemsep,0,\
  to $wims_menu_items
!endif

:end
<hr style="width:40%;">
</div>
!tail
