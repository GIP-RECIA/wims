!!##!! language independant
!read adm/class/classname.phtml $wims_classname\
$wims_institutionname

!read adm/title.phtml 1\
$classname\
$livret_title\
$name_job

<hr style="width:40%">
<ul>
 <li>$name_title_comp : <span style="color:green;" class="bold">$(title_$comp)</span>.</li>
!let eold=!record $palier of wimshome/log/classes/$wims_class/livret/.comp$comp
!distribute line $eold into name,eold
 <li>$name_n_Palier $palier : <span style="color:green;" class="bold">$name</span>.</li>
!if $eold!=
  !read proc/legend.proc legend
  !set table_id=TABLE_user
  !set table_class=sortable
  $table_header
  <thead>
  $table_hdtr
    <th scope="col">$wims_name_Sheet</th>
    <th scope="col">$wims_name_Exercise</th>
    <th scope="col">$wims_name_title</th>
    <th scope="col">&nbsp;</th>
  </tr>
  </thead>
  <tbody>
  !for l in $eold
    !let t=!replace internal . by , in $l
    !if $(t[1]) notitemof $livret_nonactivesheet
      $table_tr
       <td>
      !if $(t[1]) isitemof $nssheets
        $(t[1])
      !else
        !href module=adm/sheet&sh=$(t[1]) $(t[1])
      !endif
      </td><td>
      !if $(t[1]) notitemof $nssheets
        !set ex=!record $(t[2]) of wimshome/log/classes/$wims_class/sheets/.sheet$(t[1])
        !distribute lines $ex into di,pa,require,we,ti,de
        !if $(deps_$(l[1]))=$empty
!! TODO indiv : maybe problem with individualisation of sheet with techvar.
          !let requires=!getscorerequire sheet=$(t[1])
          !let scores=!getscore sheet=$(t[1])
          !let means=!getscoremean sheet=$(t[1])
          !readproc adm/mkdepsheet.proc $(t[1])
          !let deps_$(t[1])=$deps
        !endif
        !set dep=!row $(t[2]) of $(deps_$(t[1]))
        !set dep=!trim $dep
        !if $dep=$empty
          !href module=$di&cmd=new&worksheet=$(t[1])&$pa $(t[2])
        !else
          $(t[2])
        !endif
      !else
        $(t[2])
      !endif
      </td><td>
      !line $(t[2]) of $(sheet$(t[1]))
      </td>
      !let t=!word $(t[2]) of $(score[$(t[1]);])
      !readproc adm/class/colors $t,livret
      <td style=$style_note data-sort="$value_note">
       $output_note
      </td>
      </tr>
    !endif
  !next l
  </tbody>
  $table_end
  !read tablesort.phtml
!endif
 </li>
</ul>

!set wims_menu_items=!append line back2,1,cmd=renew\
to $wims_menu_items
