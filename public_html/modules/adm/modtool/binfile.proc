!read scripts/getindex.proc $modreq
!if $title=$empty and $inddef=$empty
 job=modname
 modreq=
 !changeto $job.proc
!endif

dir=binary,data,images,help,anstype,slib
cattest=!module category devel/$auth_login/$mod
!if document isin $cattest
  dir=doc/srctex
  test=!defof common_files in wimshome/public_html/$devdir/$mod/var.proc
!!!FIXME depends on 1
  !if $test!=yes
    dir=!append item doc/1/files to $dir
  !else
    dir=!append item doc/files to $dir
  !endif
!endif
!if oef isin $cattest
 test=!defof common_images in wimshome/public_html/$devdir/$mod/var.proc
 !if $test!=yes and datamodule/ notin $test
  oefs=!sh cd $devdir/$mod/src\
   ls *.oef | grep -v '\*' | sed 's/\.oef$$//' | awk '{print "images/"$$1}'
  oefs=!lines2items $oefs
  dir=$dir,$oefs
 !endif
!endif

!bound bin_dir within $dir default $

!if $bin_job=erase and $fname!=$empty
  check=!translate internal / to , in $fname
  check=!item 1 to -2 of $check
  check1=$(check[1])
  check=!nospace $check
  check=!translate internal , to / in $check
  !if $check!=$empty and .. notin $fname and \
    ($check isitemof $dir or $check1 iswordof slib)
    src=cd $devdir/$mod; rm -f $fname
    !if ../images/ isin ../$check/
      src=$src\
      exec imgindex
    !endif
    !sh $src
    successdelete=yes
  !else
    successdelete=no
  !endif
!endif

success=no
!if $wims_deposit!=$empty and $wims_deposit!=noname.file \
    and $bin_job=deposit and $bin_dir!=$empty \
    and / notin $wims_deposit and .. notin $wims_deposit and & notin $wims_deposit
  wims_deposit=!nospace $wims_deposit
  wims_deposit=!deaccent $wims_deposit
  !set test=!filelist $devdir/$mod/images
  !set test1=!lowercase $test
  !set test2=!lowercase $wims_deposit
  !if $test2 isin $test1 and $wims_deposit notin $test
    !!files are different only by the case
    error=bad_case
    !goto list
  !endif
  src=mkdir $devdir/$mod/$bin_dir\
    mv $wims_home/$wims_sesdir/user-deposit $devdir/$mod/$bin_dir/$wims_deposit
  !if ../images/ isin ../$bin_dir/
    src=$src\
    cd $devdir/$mod\
    exec imgindex
  !endif
  !sh $src
  success=yes
!endif
:list
dirs=!items2words $dir
bfiles=!sh cd $devdir/$mod\
  dd=`find $dirs -type f -name '*'| sort | uniq 2>/dev/null`\
  [ -z "$$dd" ] && exit\
  ls -l $$dd | awk '{print $$9","$$5}'

bcnt=!linecnt $bfiles

dest_=$wims_home/public_html/$devdir/$mod

!if $mkdoc !=$empty
  !sh cd $dest_ ; $wims_home/other/bin/src2def ;
!endif

!reset mkdoc
