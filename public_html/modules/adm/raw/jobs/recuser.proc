# RecUser
# Recover : Retrouve l'utilisateur $quser precedemment supprime.

!read scripts/check.class
!if $error!=$empty
  !exit
!endif

class_superclass=!defof class_superclass in $classdeffile

# On ajoute pas un utilisateur dans une sous-classe. toujours dans sa superclasse.
!if $class_superclass != $empty
  userdir=wimshome/log/classes/$class_superclass/.users
  userdir2=$wims_home/log/classes/$class_superclass/.users
!else
  userdir=wimshome/log/classes/$qclass/.users
  userdir2=$wims_home/log/classes/$qclass/.users
!endif


quser=!word 1 of $quser
quser=!translate internal . to @ in $quser
quser=!text select $char_login in $quser
l=!charcnt $quser
!if $quser=supervisor or $l<$login_min or $l>$login_max
  error=bad login name
  !exit
!endif

test=!defof user_exists in $userdir/$quser
!if $test!=$empty
  error=undeleted user exists
  !exit
!endif

test=!defof user_exists in $userdir/.$quser
!if $test=$empty
  error=no deleted user found
  !exit
!endif

!sh cd $userdir2; mv .$quser $quser >/dev/null

folder_list=!defof USER_DIR_LIST in wimshome/public_html/bases/sys/define.conf
!for folder in $folder_list
  !sh cd $wims_home/log/classes/$qclass/$folder\
      mv .$quser $quser
  !if $folder issametext score
    !sh cd $wims_home/log/classes/$qclass/$folder\
        mv .$quser.bin $quser.bin\
        mv .$quser.exam $quser.exam
  !endif
!next

!read adm/class/mkuserlist $qclass
!read adm/class/stat
