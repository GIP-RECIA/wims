!goto $wims_read_parm

:def
title=Coefficients de corrélation linéaire
synonyme=correlation lineaire, coefficients de correlation
input=data2d
!exit

:proc
!if $cnt<2
 !exit
!endif
pari_header=data=[$fml2];\
	meanx=sum(t=1,$cnt,data[t,1])/$cnt;\
	meany=sum(t=1,$cnt,data[t,2])/$cnt;\
	sigmax=sqrt(sum(t=1,$cnt,(data[t,1]-meanx)^2));\
	sigmay=sqrt(sum(t=1,$cnt,(data[t,2]-meany)^2));\
	cov=sum(t=1,$cnt,(data[t,1]-meanx)*(data[t,2]-meany));
	
result=!exec pari print(cov/(sigmax*sigmay));\
	print(cov);\
	print(sigmax);\
	print(sigmay);\
	print(meanx);\
	print(meany);
	
result=!nonempty lines $result
test=!linecnt $result
!if $test<6
 result=
 !exit
!endif
!distribute lines $result into corr,cov,sigmax,sigmay,meanx,meany
!exit

:output
<table class="wimsnoborder">
<tr><td nowrap align="right">
 Coefficients de corrélation linéaire = <td>
 !htmlmath $corr
<tr><td nowrap align="right">
 Covariance cov(x,y) = <td>
 !htmlmath $cov
<tr><td nowrap align="right">
 ($m_sigma<sub>x</sub> , $m_sigma<sub>y</sub>) = <td>
 !htmlmath ($sigmax , $sigmay)
<tr><td nowrap align="right">
 Moyenne = <td>
 !htmlmath ($meanx , $meany)
</table>

Données : $cnt couples { $fml3 }. Ces données ont donc une corrélation
!item $[floor(abs($corr)*7)+1] of pratiquement inéxistante,\
	très faible, plutôt faible, moyenne, plutôt forte, très forte,\
	très très forte
.
<p>
<b>Rappel</b>. Les coefficients de corrélation linéaire d'un ensemble
</p>
<p class="wimscenter">
{($m_x<sub>1</sub>,$m_y<sub>1</sub>) , ... , ($m_x<sub>$m_n</sub>,$m_y<sub>$m_n</sub>)}
</p>
de données discrètes de dimension 2 sont définies par
 <div class="wimscenter">
!insmath \rho_{xy} = {{\rm cov}(x,y) \over \sigma_x \sigma_y }
</div>
où 
!set ins_align=middle
!insmath {\rm cov}(x,y) = \sum_{i=1}^n (x_i - m_x)(y_i - m_y)
 est la <em>covariance</em>, 
!insmath \sigma_x=\sqrt{\sum_{i=1}^n (x_i - m_x)^2}
 et
!insmath \sigma_y=\sqrt{\sum_{i=1}^n (y_i - m_y)^2}
 sont les <em>variances</em> en x et en y, et 
 ($m_m<sub>$m_x</sub>,$m_m<sub>$m_y</sub>) est la moyenne des données.
!exit

