!if $wims_read_parm!=slib_header
  !goto proc
!endif

slib_parms=3\
,[data]\
0.5,p\
1000,M

slib_author=Sophie, Lemaire; Bernadette, Perrin-Riou

slib_example=[0.2,0.4,0.7],0.5
!exit

:proc

!distribute  item $wims_read_parm into slib_data,slib_p,slib_N
!default slib_p=0.5
!default slib_N=1000
!if  $slib_p<=0  or $slib_p>=1
slib_out=0
!exit
!endif
slib_out=
slib_data=!declosing $slib_data

slib_cnt= !itemcnt $slib_data
slib_p=$[$slib_p]
slib_pp=$[1-$slib_p]
!for slib_i =1 to $slib_cnt
	slib_di=!item $slib_i of $slib_data
	slib_P=0
	slib_a=$slib_p
	!for slib_k=1 to $slib_N
		slib_P=$[$slib_P+$slib_a]
		!if $slib_P>= $slib_di
			slib_out=!append item  $slib_k to $slib_out
			!break
		!endif
		slib_a=$[$slib_a*$slib_pp]
	!next slib_k
!next slib_i
slib_out=!trim $slib_out
