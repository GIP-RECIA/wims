
!if $sstyle=1
  !if $substep=0
    !if $all_steps=0
      !form reply
      Quelle est la <em>première</em> étape erronée de cette série&nbsp;?
      !formselect badstep from 0 to $stepcnt
      (0 voudra dire que tout est juste.)
      <div class="wimscenter">
      <input type="submit" value="Envoyer la réponse"></div>
    !else
      Marquer <em>toutes</em> les mauvaises étapes ci-dessus, puis
      <input type="submit" value="cliquer ici">.
    !endif
    !formend
  !else
    !if $all_steps=0
      !if $badstep=0
        Effectivement, toutes les étapes sont correctes.
      !else
        En effet, la première mauvaise étape est le numéro $badstep.
      !endif
    !endif
    Votre note&nbsp;: $module_score/10.
    !if $expstep!=$empty
      <p>Quelques explications.
      </p><ul>
      !for i in $expstep
        <li>L'étape $i
        !if $i isitemof $badstep
          est correcte.
          !line $i of $remarks
        !else
          !set j=!positionof item $i in $bad_steps
          !set e=!item $j of $bad_reasons
          n'est pas bonne.
          !read reasons/$e.det
        !endif
        </li>
      !next i
      </ul>
    !endif
  !endif
!else
  !if $substep=0
    !form reply
    Que pensez-vous de la <em>dernière</em> étape&nbsp;?
    !formselect ans list --,OK,$bad_reasons prompt Je ne sais pas,\
    	Oui : cette étape est correcte,$bad_rnames
    <input type="submit" value="Envoyer">
    !formend
  !endif

  !if $substep=2
    Vous avez raison&nbsp;:
    !if $step_reason=OK
      la dernière étape est correcte.
    !else
      la dernière étape est erronée.
      La bonne étape sera
      <div class="wimscenter">
      !line $step of $good
      </div>
    !endif
    <p>
    !if $step<$stepcnt
      !href cmd=reply&ans=next Passer à l'étape suivante.
    !else
      Cet exercice est terminé. Votre note&nbsp;: $module_score/10.
    !endif
    </p>
  !endif
!endif
