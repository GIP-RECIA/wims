!set anstype=yes
!set anstyle=mc
!set ans_require=

!if $wims_read_parm=def
  !exit
!endif
!set input_rg=!rows2lines $(replygood$i)
!set input_1=!line 1 of $input_rg
!set input_rg=!line 2 to -1 of $input_rg
!set input_icnt=!itemcnt $input_rg
!if $input_icnt<2
  !exit
!endif
!if debug iswordof $m_oefenv and ?analyze notin $(replygood$i) \
	  and $(reply$i)=$empty
  !set replydefaulted=yes
  !if $oef_default=yes
    !default reply$i=!item $input_1 of $input_rg
    !! to be compatible with the same transformation for input_rg2
    !set reply$i=!replace internal \( by \\( in $(reply$i)
  !endif
!endif
!if shuffle iswordof $(replyoption$i)
  !default anstype_shuffle_$i=!shuffle $input_icnt
  !set input_rg=!item $(anstype_shuffle_$i) of $input_rg
!endif
!if sort iswordof $(replyoption$i)
  !set input_rg=!sort items $input_rg
!endif

!set input_rg2=!replace internal \( by \\( in $input_rg

!if $wims_read_parm!=noprompt
  <tr><td>$(replyname$i)</td><td>
!endif

!bound inputsize between integer 1 and 100 default $
!if $inputsize!=$empty and $inputsize<=$input_icnt
  !set menuitem=!item $inputsize of $input_rg2
  !default prompt_=!item $inputsize of $input_rg
  !if $(input_rg2[$inputsize]) isitemof $(reply$i)
    <input type="radio" name="reply$i" id="reply$(i)_$inputsize" value="$menuitem" checked="checked">
  !else
    <input type="radio" name="reply$i" id="reply$(i)_$inputsize" value="$menuitem">
  !endif
  !if nolabel iswordof $(replyoption$i)
    $prompt_
  !else
    <label for="reply$(i)_$inputsize">$prompt_</label>
  !endif
!else
  !formradio reply$i list $input_rg2 prompt $input_rg
!endif

!if $wims_read_parm!=noprompt
  </td></tr>
!endif
!reset inputsize
!set oef_answer_option$i=$(inputprompt$i)
