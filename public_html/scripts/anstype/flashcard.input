!set ans_require=
!reset option2 option1
!set anstype=yes
!set anstyle=mc

!if $wims_read_parm=def
  !exit
!endif


!set style=.wims_flashcard{\
  display:inline-block;\
  min-width:30px;\
  min-height:30px;\
  padding:10px 3px 5px 3px;\
  font-size:24px;\
  cursor:pointer;\
  transition: opacity .5s;\
}\
.wims_flashcard:hover{opacity:.66;}\

!!define if above css has always been set in DOM, to avoid setting twice.
!if $style notin $oef_anstype_css
  !set oef_anstype_css=!append line $style to $oef_anstype_css
!endif


!set inputoption=!replace internal $	$ by $\
$ in $inputsize
!set input_no$i=!line 1 of $inputoption
!set input_no$i=!item 1 of $(input_no$i)
!set inputoption=!line 2 to -1 of $inputoption
!set option1=!line 1 of $inputoption
!set option2=!line 2 of $inputoption
!default option2=$option1
!distribute items #9999CC,#FDFF94 into color1,color2

!default option1=style="background-color:$color1;"
!default option2=style="background-color:$color2;"

!set oef_fill_option=$option1\
$option2

!set input_rg=!rows2lines $(replygood$i)
!set input_1=!word 1 of $input_rg
!if $input_1!=?analyze
  !set input_rg=!line 2 to -1 of $input_rg
!endif

!set input_rg=!translate internal | to $\
$ in $input_rg
!set input_1=!line 1 of $input_rg
!set input_icnt$i=!itemcnt $input_1
!set prompt$i=!line 2 of $input_rg

!if shuffle iswordof $(replyoption$i)
  !default anstype_shuffle_$i=!shuffle $(input_icnt$i)
  !set input_1=!item $(anstype_shuffle_$i) of $input_1
!endif
!if sort iswordof $(replyoption$i)
  !set input_1=!sort items $input_1
!endif

!if show iswordof $(replyoption$i)
  !set show=yes
!endif
!default show=no

!default prompt$i=??

!if $(input_no$i)=1 or $(input_no$i)=$empty
  <script>
  /*<![CDATA[*/
  var n$i=$(input_icnt$i);
  var click$i=0;
  var input$i = new Array();
  var click$i = new Array();
  !for s_=0 to $[$(input_icnt$i)-1]
    input$i[$s_]='$(input_1[$s_+1])'
    click$i[$s_]='0'
  !next
  function flashcard_on$i(s){
      var fl = document.getElementById('flashcard$i'+s)
     if (fl.innerHTML == '$(prompt$i)') {
        fl.innerHTML = input$i[s];
        fl.style.backgroundColor='$color1';
        click$i[s] ++
      }
     else {
      fl.innerHTML = '$(prompt$i)';
      fl.style.backgroundColor='$color2';
      click$i[s] ++
     };
     fieldsend$i();
  }

  !if $show=yes
    function fieldsend$i(){
      	var rep;var reply=''; var click_for_wims='';
	      for(var p=0;p<n$i;p++){
	      var p1=p+1;
	      rep=document.getElementById('flashcard$i'+p).innerHTML;
	  	if (rep=='$(prompt$i)') { if (reply !='' ){ reply=reply+',';} ; reply=reply+p1;}
	  	if (click_for_wims !='' ){click_for_wims=click_for_wims+','} ; click_for_wims=click_for_wims+click$i[p];
       }
        document.getElementById('oefvar$i').value=reply+';'+click_for_wims;
    }
  !else
    function fieldsend$i(){
      	var rep;var reply=''; var click_for_wims='';
	      for(var p=0;p<n$i;p++){
	      var p1=p+1;
	      rep=document.getElementById('flashcard$i'+p).innerHTML;
	  	if (rep!='$(prompt$i)') { if (reply !='' ){ reply=reply+',';} ; reply=reply+p1;}
	  	if (click_for_wims !='' ){click_for_wims=click_for_wims+','} ; click_for_wims=click_for_wims+click$i[p];
       }
        document.getElementById('oefvar$i').value=reply+';'+click_for_wims;
    }
  !endif
  /*]]>*/
  </script>
  <input type="hidden" name="reply$i" id="oefvar$i" value="">
!endif

!if $(input_no$i)!=$empty and $(input_no$i)<=$(input_icnt$i)
  !if $show=yes
    <div id="flashcard$i$[$(input_no$i)-1]" class="wims_flashcard" onclick="flashcard_on$i('$[$(input_no$i)-1]')" $option2>$(input_1[$(input_no$i)])</div>
  !else
    <div id="flashcard$i$[$(input_no$i)-1]" class="wims_flashcard" onclick="flashcard_on$i('$[$(input_no$i)-1]')" $option2>$(prompt$i)</div>
  !endif
!else
  !for s_=0 to $[$(input_icnt$i)-1]
    !if $show=yes
      <div id="flashcard$i$[$(input_no$i)-1]" class="wims_flashcard" onclick="flashcard_on$i('$s_')" $option2>$(input_1[$s_+1])</div>
    !else
      <div id="flashcard$i$[$(input_no$i)-1]" class="wims_flashcard" onclick="flashcard_on$i('$s_')" $option2>$(prompt$i)</div>
    !endif
    &nbsp;
  !next
!endif

!set oef_js_submit=$oef_js_submit fieldsend$i();
