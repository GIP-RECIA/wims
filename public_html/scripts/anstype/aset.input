!set anstype=yes
!set anstyle=numeric
!set ans_require=
!if $wims_read_parm=def
  !exit
!endif
!if debug iswordof $m_oefenv and ?analyze notin $(replygood$i)
  !set replydefaulted=yes
  !if $oef_default=yes
    !set r_=!declosing $(replygood$i)
    !if , notin $good
      !set r_=!words2items $r_
    !endif
    !if repeat notwordof $(replyoption$i)
      !default reply$i=!listuniq $r_
    !else
      !default reply$i=$r_
    !endif
  !endif
!endif
!if $wims_read_parm!=noprompt
  <tr><td><label for="reply$i">$(replyname$i)</label>&nbsp;=</td><td>
!endif

!read anstype/inputcss.inc

!bound inputsize between integer 1 and 100 default 30
!set rr_=!declosing $(reply$i)
!set rg_cnt=!declosing $(replygood$i)
!set rg_cnt=!itemcnt $rg_cnt
{
!if distinct_inputs iswordof $(replyoption$i)
  <input type="hidden" name="reply$i" id="reply$i" value="">
  !for l_ = 1 to $rg_cnt
    <input list="emptylist" id="oefvar$(i)_$l_" size="$inputsize" name="oefvar$(i)_$l_" value="$(rr_[$l_])" autocapitalize="none" $inputoption>
    !if $l_<$rg_cnt
,
    !endif
  !next
  <script>
  /*<![CDATA[*/
    function show_set$i(){
     document.getElementById('reply$i').value=document.getElementById('oefvar$(i)_1').value;
     for(var p = 2 ; p <= $rg_cnt ; p++){
      document.getElementById('reply$i').value += ',' + document.getElementById('oefvar$(i)_' + p).value;
     }
    }
  /*]]>*/
  </script>
  !set oef_js_submit=$oef_js_submit show_set$i();
!else
  <input list="emptylist" id="reply$(i)" size="$inputsize" name="reply$(i)" value="$rr_" autocapitalize="none" $inputoption>
  !if symbols isin $(replyoption$i)
    !read anstype/symbols.inc
  !endif
!endif
}
!if $wims_read_parm!=noprompt
  </td></tr>
!endif
!reset inputsize inputoption option
