# make some list of varfilter in a structure (class or groupement or portal)
# variable defined are :
# tv_Sclass : dir of .indextechvar file in the structure.
# tv_listallname : list of all name of techvar in structure.
# tv_listname : list of name of techvar available at the actual level of a structure.
# tv_listlocalname : list of name of local techvar.
# tv_listtechvar : list (of line) for each techvar available (with information on each techvar in each line : name,classid,number)
# tv_listlocal : list of num of local techvar (in the list tv_listtechvar)
# tv_varfilterreaden : yes when read this file
# tv_listcode : list of codes (in .indextechvar) for each technical variable
# tv_listlocalcode : list of codes (in .indextechvar) for local technical variable
# to find the values of a techvar, one can do something as
###  !let don=!line $k of $tv_listtechvar
###  !let don2=!record $(don[3]) of wimshome/log/classes/$(don[2])/.techvar
###  !distribute line $don2 into name,val

!if $tv_varfilterreaden=yes
  !exit
!endif
tv_varfilterreaden=yes
!if $tv_Sclass!=$empty
  !exit
!endif
!reset tv_Sclass,tv_listallname,tv_listname,tv_listtechvar,tv_listlocal,tv_listcode
!if $wims_superclass!=$empty
  tv_Sclass=$wims_superclass
  tmp_=!replace / by , in $wims_class
  n_=!itemcnt $tmp_
  parent_=$wims_superclass
  !if $n_>1
    !for k_=2 to $n_
      parent_=!append item $(parent_[-1])/$(tmp_[$k_]) to $parent_
    !next k_
  !endif
!else
  tv_Sclass=$wims_class
  parent_=$wims_class
!endif
nbtechvar_=!recordcnt wimshome/log/classes/$tv_Sclass/.indextechvar
!if $nbtechvar_>0
  !for k_=1 to $nbtechvar_
    tmp_=!record $k_ of wimshome/log/classes/$tv_Sclass/.indextechvar
    tv_listallname=!append item $(tmp_[1]) to $tv_listallname
    !if $(tmp_[2]) isitemof $parent_
      tv_listtechvar=!append line $tmp_ to $tv_listtechvar
      tv_listname=!append item $(tmp_[1])$ to $tv_listname
      tn_=!replace internal / by _ in $(tmp_[2])
      tv_listcode=!append item $(tn_)_$(tmp_[3]) to $tv_listcode
      !if $(tmp_[2])=$wims_class
        t_=!linecnt $tv_listtechvar
        tv_listlocal=!append item $t_ to $tv_listlocal
      !endif
    !endif
  !next k_
!endif
tv_listlocalname=!item $tv_listlocal of $tv_listname
tv_listlocalcode=!item $tv_listlocal of $tv_listcode
