!set methtit=Suma de desigualdades con coeficientes
!set methenv=ZZ QQ RR
!set methparmtype=parm inequality >,parm inequality >,\
	number, number
!set methhelp=Este método acepta dos desigualdades existentes en\
la hipótesis, para deducir una tercera, que es\
la suma con coeficientes de los dos primeros.\
La nueva desigualdad se añadirá en las hipótesis.

!if $wims_read_parm iswordof form check
  !goto $wims_read_parm
!endif

!exit
:form
Introduciruna nueva desigualdad que es <br>
!default methparm3=1
!default methparm4=1
!read deduc/methparm.phtml 3,5
$ veces la desigualdad
!read deduc/methparm.phtml 1
<br> más
!read deduc/methparm.phtml 4,5
$ veces la desigualdad
!read deduc/methparm.phtml 2

!exit
:check
!ifval $methparm3=0 and $methparm4=0
  error=¡Los coeficientes son cero!
  !exit
!endif
!distribute items $methparmobj1 into data1,left1,sign1,right1
!distribute items $methparmobj2 into data2,left2,sign2,right2
!if $data1 issametext $data2
  error=Este método prohíbe la combinación lineal de una desigualdad\
  consigo misma.
  !exit
!endif
methexp=\(($methparm3)*($left1 $sign1 $right1) + ($methparm4)*($left2 $sign2 $right2))
!if $methparm3<0
  methparm3=-($methparm3)
  sign1=!translate internal <> to >< in $sign1
  left1=-($left1)
  right1=-($right1)
!endif
!if $methparm4<0
  methparm4=-($methparm4)
  sign2=!translate internal <> to >< in $sign2
  left2=-($left2)
  right2=-($right2)
!endif

gt=>
lt=<
!if ($gt isin $sign1 and $lt isin $sign2) or\
	($lt isin $sign1 and $gt isin $sign2)
  !exchange left2,right2
!endif
!read deduc/sub/simplify ($methparm3)*($left1) + ($methparm4)*($left2)\
	($methparm3)*($right1) + ($methparm4)*($right2)
!distribute lines $out into newleft, newright
!if $newleft=$empty or $newright=$empty
  error=bad_data
  !exit
!endif

!if $gt isin $sign1
  s_=>
!else
  s_=<
!endif
eq=$empty=
!if ($eq isin $sign1 or $[$methparm3]=0) \
	and ($eq isin $sign2 or $[$methparm4]=0)
  s_=$s_=
!endif
newobject0=$newleft $s_ $newright
oldobject=0

contextlist=$ct_Always,\
	$ct_Onlywhen ambos coeficientes son distintos de cero,\
	$ct_Onlywhen ambas desigualdades son positivas,\
	$ct_Onlywhen una de las dos desigualdades es positiva,\
	$ct_Onlywhen una de las dos desigualdades tiene el mismo signo

!exit
